#!/usr/bin/perl -w

=head1 NAME

dh_pyppd - Compress the CUPS PPDs using pyppd.

=cut

use strict;
use Debian::Debhelper::Dh_Lib;
use File::Path;
use File::Find;

=head1 SYNOPSIS

B<dh_pyppd> [S<I<debhelper options>>]

=head1 DESCRIPTION

B<dh_pyppd> is a debhelper program that compresses PPDs installed under
/usr/share using pyppd, removes them from the binary package and adds the
needed Depends to the package's \${misc:Depends}.
ยง
=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $ppd_src="$tmp/usr/share/";
	my $pyppd_dest="$tmp/usr/lib/cups/driver";
	my $package_printer_driver=$package;
	$package_printer_driver =~ s/^printer-driver-//;

	if(-d $ppd_src) {
		my $ppds_presence = 0;
		find(sub { if(($_ =~ m/\.ppd(\.gz)?$/) ) {
				$ppds_presence++;
			   }
			  }, "$ppd_src");

		if( $ppds_presence ) {
			# verbose_print("PPDs found in $ppd_src.");

			# verbose_print("Creating pyppd archive at $pyppd_dest/$package.");
			mkpath($pyppd_dest);
			doit("pyppd","--output=$pyppd_dest/$package_printer_driver","$ppd_src");

			# verbose_print("Remove PPDs now in the archive.");
			doit("find",$ppd_src,"-name","\*.ppd","-delete","-o","-name","\*.ppd.gz","-delete");

			# verbose_print("Remove empty directories left.");
			doit("find",$ppd_src,"-type","d","-empty","-delete");

			# verbose_print("Add needed Depends in \${misc:Depends}");
			addsubstvar($package,"misc:Depends", "xz-utils");
			addsubstvar($package,"misc:Depends", "python3");
		}
		else {
			# verbose_print("No PPDs found under $ppd_src.");
		}
	}
}

=head1 AUTHOR

Didier Raboud <odyx@debian.org>

=cut
